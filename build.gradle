plugins {
    id "dev.architectury.loom" version "1.1-SNAPSHOT"
}

sourceCompatibility = targetCompatibility = JavaVersion.VERSION_1_8

archivesBaseName = archives_base_name
version = mod_version
group = maven_group

loom {
    accessWidenerPath = file("src/main/resources/animatica.accesswidener")
    silentMojangMappingsLicense()
    forge {
        convertAccessWideners = true
        mixinConfigs = [
            "animatica.mixins.json"
        ]
    }
    mixin.defaultRefmapName.set("animatica.refmap.json")
}

repositories {
    maven {
        url "https://cursemaven.com"
        content {
            includeGroup "curse.maven"
        }
    }
}

dependencies {
    minecraft "com.mojang:minecraft:${minecraft_version}"
	mappings "net.fabricmc:yarn:${yarn_mappings}:v2"
    forge "net.minecraftforge:forge:${forge_version}"
    modImplementation("curse.maven:embeddium-908741:4803825")
    modImplementation("curse.maven:potacore-951521:4970520")
}

processResources {
    inputs.property "version", project.version
    filesMatching("META-INF/mods.toml") {
        expand "version": project.version
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = "UTF-8"
    options.release.set(8)
}

jar {
    manifest {
        attributes([
                "Implementation-Title"    : name,
                "Implementation-Version"  : mod_version,
                "Implementation-Vendor"   : mod_author,
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}
